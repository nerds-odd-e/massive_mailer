def doMysql(String target) {
    exec {
        executable "sh"
        args "-c", "mysql -u root -e " + target
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.8"
        classpath group: 'org.javalite', name: 'activejdbc-gradle-plugin', version: '2.2'
    }
}

ext {
    junit5Version = '5.3.0'
    groovyVersion = '2.4.5'
    gebVersion = '1.1.1'
    seleniumVersion = '3.7.1'
    cucumberJvmVersion = '1.2.5'
    assertJVersion = "3.10.0"
    ciBuild = System.getenv('SNAP_CI')
}

apply plugin: "com.github.samueltbrown.cucumber"
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.javalite.activejdbc'
apply from: 'gradle/gretty.gradle'
apply from: "gradle/idea/idea.gradle"

group = 'com.odde'
version = '0.0.1-SNAPSHOT'
description = """massive_mailer"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
war.baseName = 'massive_mailer'


configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

gretty {
    httpPort = 8070
    if(project.gradle.startParameter.taskNames.any{e -> e.startsWith('cucumber')}) {
        doMysql("\"drop database massive_mailer_test;\"")
        doMysql("\"create database massive_mailer_test;\"")
        jvmArgs = ["-Dactive_env=test"]
        httpPort = 8060
    }
}

cucumber {
    formats = [
            'pretty', // prints nice format out to the console
            'html:build/reports/cucumber', // html
            'junit:build/cucumber.xml' // junit format for integration with CI tool etc
    ]
    // src/cucumber/resources is included automatically
    glueDirs = [
            "classpath:io.jdev.geb.cucumber.steps.groovy.en", 'classpath:steps'
    ]

    tags = ['~@system', '~@slow', '~@developing']
    strict = true
}

tasks.cucumber {
    dependsOn assemble, testClasses, 'appBeforeIntegrationTest'
    finalizedBy 'appAfterIntegrationTest'

    jvmOptions.systemProperties("webdriver.chrome.driver": "/usr/local/bin/chromedriver")
    jvmOptions.systemProperties([
            "geb.cucumber.step.packages": "steps",
            "geb.env": System.getProperty("geb.env"),
            "webdriver": "chrome",
            "active_env": "test",
    ])
}

task cucumber_now {
    group "verification"
    finalizedBy "cucumber"
    doFirst {
        cucumber {
            tags = ['@now']
        }
    }
}

test {
  useJUnitPlatform()
}

dependencies {
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.4'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
    compile group: 'com.google.code.gson', name: 'gson', version: '1.7.1'
    compile group: 'com.icegreen', name: 'greenmail', version: '1.4.1'

    testCompile("org.junit.jupiter:junit-jupiter-api:$junit5Version")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit5Version")
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testRuntime("org.junit.vintage:junit-vintage-engine:$junit5Version")
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.springframework', name: 'spring-mock', version: '2.0.8'
    testCompile group: 'org.springframework', name: 'spring-core', version: '2.0.8'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.20'

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.javalite', name: 'db-migrator-maven-plugin', version: '1.4.11'
    compile group: 'org.javalite', name: 'activejdbc', version: '2.2'

    testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-server:$seleniumVersion"
    testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
    testCompile "info.cukes:cucumber-java:$cucumberJvmVersion"
    testCompile "info.cukes:cucumber-junit:$cucumberJvmVersion"
    testCompile "org.assertj:assertj-core:$assertJVersion"
    compile 'org.flywaydb:flyway-core:3.2.1'

    compile group: "com.google.maps", name: "google-maps-services", version: "0.2.4"
}

apply from: "gradle/ci.gradle"
